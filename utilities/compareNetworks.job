
RUN PGM=NETWORK

  NETI[1] = "%NET1_FILE%"
  NETI[2] = "%NET2_FILE%"
  NETO    = "COMPARE_NET.net"
  MERGE RECORD = TRUE

  PHASE=INPUT
    _NODE_MISMATCH=0
    _LINK_MISMATCH=0
  ENDPHASE

  PHASE=NODEMERGE
    IN_NET1 = 1
    IN_NET2 = 1
    IF (NI.1.X=0 && NI.1.Y=0) IN_NET1 = 0
    IF (NI.2.X=0 && NI.2.Y=0) IN_NET2 = 0

    IF (IN_NET1=1 & IN_NET2=0)
      MISMATCH = 'DELETED'
    ELSEIF (IN_NET1=0 & IN_NET2=1)
      MISMATCH = 'ADDED'
    ELSE
      MISMATCH = ''
      IF (NI.1.COUNTY    <> NI.2.COUNTY    ) MISMATCH = MISMATCH + 'COUNTY '
      IF (NI.1.MODE      <> NI.2.MODE      ) MISMATCH = MISMATCH + 'MODE '
      IF (NI.1.FAREZONE  <> NI.2.FAREZONE  ) MISMATCH = MISMATCH + 'FAREZONE '
    ENDIF

    IF (STRLEN(MISMATCH) > 0)
        _NODE_MISMATCH = _NODE_MISMATCH + 1
        print LIST="NODE MISMATCH: ",MISMATCH,"; N=",N(LRS)," IN_NET1=",IN_NET1(LRS)," IN_NET2=",IN_NET2(LRS)

        print LIST="COUNTY   : ",NI.1.COUNTY  ," ", NI.2.COUNTY
        print LIST="MODE     : ",NI.1.MODE    ," ", NI.2.MODE
        print LIST="FAREZONE : ",NI.1.FAREZONE," ", NI.2.FAREZONE

    ENDIF
  ENDPHASE

  PHASE=LINKMERGE
    IN_NET1 = 1
    IN_NET2 = 1
    IF (LI.1.A=0 && LI.1.B=0) IN_NET1 = 0
    IF (LI.2.A=0 && LI.2.B=0) IN_NET2 = 0

    ; Keep these
    N1_NAME      = LI.1.NAME
    N1_FEET      = LI.1.FEET
    N1_CNTYPE    = LI.1.CNTYPE
    N1_USECLASS  = LI.1.USECLASS
    N1_TOLLBOOTH = LI.1.TOLLBOOTH
    N1_TOLLSEG   = LI.1.TOLLSEG
    N1_FT        = LI.1.FT
    N1_NUMLANES  = LI.1.NUMLANES

    N2_NAME      = LI.2.NAME
    N2_FEET      = LI.2.FEET
    N2_CNTYPE    = LI.2.CNTYPE
    N2_USECLASS  = LI.2.USECLASS
    N2_TOLLBOOTH = LI.2.TOLLBOOTH
    N2_TOLLSEG   = LI.2.TOLLSEG
    N2_FT        = LI.2.FT
    N2_NUMLANES  = LI.2.NUMLANES

    IF (IN_NET1=1 & IN_NET2=0)
      MISMATCH = 'DELETED'
    ELSEIF (IN_NET1=0 & IN_NET2=1)
      MISMATCH = 'ADDED'
    ELSE
      MISMATCH = ''
      IF (ABS(LI.1.FEET - LI.2.FEET) > 0.001 ) MISMATCH = MISMATCH + 'FEET '

      IF (LI.1.NAME      <> LI.2.NAME      ) MISMATCH = MISMATCH + 'NAME '
      IF (LI.1.CNTYPE    <> LI.2.CNTYPE    ) MISMATCH = MISMATCH + 'CNTYPE '
      IF (LI.1.USECLASS  <> LI.2.USECLASS  ) MISMATCH = MISMATCH + 'USECLASS '
      IF (LI.1.TOLLBOOTH <> LI.2.TOLLBOOTH ) MISMATCH = MISMATCH + 'TOLLBOOTH '
      IF (LI.1.TOLLSEG   <> LI.2.TOLLSEG   ) MISMATCH = MISMATCH + 'TOLLSEG '
      IF (LI.1.FT        <> LI.2.FT        ) MISMATCH = MISMATCH + 'FT '
      IF (LI.1.NUMLANES  <> LI.2.NUMLANES  ) MISMATCH = MISMATCH + 'NUMLANES '
    ENDIF

    IF (STRLEN(MISMATCH) > 0)
        _LINK_MISMATCH = _LINK_MISMATCH + 1
        print LIST="LINK MISMATCH: ",MISMATCH,"; A=",A(LRS)," B=",B(LRS)," IN_NET1=",IN_NET1(LRS)," IN_NET2=",IN_NET2(LRS)

        print LIST="NAME      : ",LI.1.NAME     ," ", LI.2.NAME
        print LIST="FEET      : ",LI.1.FEET     ," ", LI.2.FEET
        print LIST="CNTYPE    : ",LI.1.CNTYPE   ," ", LI.2.CNTYPE
        print LIST="USECLASS  : ",LI.1.USECLASS ," ", LI.2.USECLASS
        print LIST="TOLLBOOTH : ",LI.1.TOLLBOOTH," ", LI.2.TOLLBOOTH
        print LIST="TOLLSEG   : ",LI.1.TOLLSEG  ," ", LI.2.TOLLSEG
        print LIST="FT        : ",LI.1.FT       ," ", LI.2.FT
        print LIST="NUMLANES  : ",LI.1.NUMLANES ," ", LI.2.NUMLANES

    ENDIF
  ENDPHASE

  PHASE=SUMMARY
    print LIST="LINK MISMATCH = ",_LINK_MISMATCH
    print LIST="NODE MISMATCH = ",_NODE_MISMATCH
  ENDPHASE
ENDRUN