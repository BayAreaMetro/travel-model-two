; ----------------------------------------------------------------------------------------------------------------
; setInterchangeDistance.job
;
; Preprocess freeway link distances to nearest major interchange.
;
; This script writes out the Cube network to CSV, calls the interchange_distance Python script to loop through links
; and calculate distances, write distances to CSV, convert to DBF, and merge into Cube network.
;
; Inputs: the input node layer needs a MAJINTX field where 0 = not a major interchange, 1 =  major interchange
;         (as set in preprocess_input_net.job)
;
; Outputs: the working network in hwy\mtc_final_network_with_tolls.net with added variables UPDIST and DOWNDIST in miles (units = DISTANCE units)
;
; mmm 2022-01-06
;-----------------------------------------------------------------------------------------------------------------

; write out freeway links
RUN PGM=NETWORK
    neti      ="hwy\mtc_final_network_with_tolls.net"
    printo[1] ="hwy\fwy_interchange_nodes.csv"
    printo[2] ="hwy\fwy_links.csv"

    phase=nodemerge
        IF (N=1) PRINT CSV=T, LIST="N","MAJINTX", PRINTO=1
        IF (N=1) PRINT CSV=T, LIST="A","B","FT","DISTANCE","INTXA","INTXB", PRINTO=2

        IF (MAJINTX > 0)
            ; major interchange node
            print CSV=T, list=N(L),"TRUE", PRINTO=1
        ENDIF

    endphase

    phase=linkmerge
        _ANODE = 0
        _BNODE = 0

        IF (FT=1 | FT=2 | FT=3) ; freeways, expressways, or ramps
          IF (A.MAJINTX > 0) _ANODE = 1
          IF (B.MAJINTX > 0) _BNODE = 1

          print CSV=T, list=A(L),B(L),FT(L),DISTANCE(6.2L),_ANODE(L),_BNODE(L), PRINTO=2
        ENDIF
    endphase
ENDRUN


; run python script to calculate distances
*"%PYTHON_PATH%\python.exe" %BASE_SCRIPTS%\preprocess\interchangeDistance.py hwy\fwy_links.csv hwy\fwy_interchange_dist.csv

; convert output CSV to DBF
*"%PYTHON_PATH%\python.exe" %BASE_SCRIPTS%\preprocess\csvToDbf.py hwy\fwy_interchange_dist.csv hwy\fwy_interchange_dist.dbf

; merge output back into network
RUN PGM=NETWORK

   ; begin with the standard input network
   neti = hwy\mtc_final_network_with_tolls.net

   ; the list of freeway links with distances
   linki[2] = hwy\fwy_interchange_dist.dbf

   ; write out to the same name with the added variables
   neto = hwy\mtc_final_network_with_tolls_dist.net

   MERGE RECORD=T

   phase=linkmerge
        DOWNDIST = LI.2.DOWNDIST
        UPDIST = LI.2.UPDIST
   endphase

ENDRUN

; overwrite network
* DEL hwy\mtc_final_network_with_tolls.net
* copy hwy\mtc_final_network_with_tolls_dist.net hwy\mtc_final_network_with_tolls.net
* DEL hwy\mtc_final_network_with_tolls_dist.net
